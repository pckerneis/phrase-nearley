Phrase {
  main = nl* listOf<macroOrStatement, nl+> nl*

  macroOrStatement = macro | statement | macroCall

  // Statement
  statement = action
  action = actionOnElement space+ element
        | actionWithString space+ string
  actionOnElement = "click"
  actionWithString = actionWithStringType
  actionWithStringType = "type" | "visit"
  element = identifier              -- single
         | "<" space* multiIdentifier space* ">"  -- multi
  multiIdentifier = nonemptyListOf<identifier, space+>

  // Macro
  macro = macroHeader nl+ macroBody
  macroHeader = inOrderTo space+ macroIdentifier ":"
  macroIdentifier = identifier (space+ macroIdentifierFragment)*
  macroIdentifierFragment = identifier | macroParameter
  macroParameter = "{" identifier "}"
  inOrderTo = "in order to"
  macroBody = nonemptyListOf<indentedStatement, emptyLine>
  indentedStatement = indent statement
  indent = space+

  macroCall = identifier

  // Lexical rules
  identifier = letter (alnum)*
  string = "\"" char* "\""
  char = escaped | unescaped
  escaped = "\\" validEscape
  validEscape = "\"" | "\\"
  unescaped = ~("\"" | "\\") any


  nl = "\n" | "\r\n" | "\r"
  space := ~nl "\x00".."\x20"
  emptyLine = nonemptyListOf<nl, space*>
}
